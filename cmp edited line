"duplicateLeads": component.get("v.leads")


// Edited method
    @AuraEnabled
    public static void mergeLeadDuplicates(Id recordId, Id masterId, List<Lead> duplicateLeads) {

        // get the duplicates leads without the master record
        List<Lead> duplicatesLeads = getDuplicates(masterId, duplicateLeads);
        Set<Id> duplicateLeadIds = getDuplicateLeadIds(duplicatesLeads);

        // get all fields from the master and duplicate records
        List<Lead> duplicates = getSObjectRecordByIdWithAllAvailableFields(Lead.class.getName(), null, duplicateLeadIds);
        List<Lead> masterLeads = getSObjectRecordByIdWithAllAvailableFields(Lead.class.getName(), masterId, null);

        if (!duplicates.isEmpty() && !masterLeads.isEmpty()) {
            Lead master = masterLeads.get(0);

            for(Lead duplicate : duplicates) {
                // merge the fields from the duplicate to the master
                Map<String, SObjectField> fields = SObjectType.Lead.fields.getMap();
                Map<String, Object> duplicateLeadValues = duplicate.getPopulatedFieldsAsMap();
                for (String field : duplicateLeadValues.keySet()) {
                    if (master.get(field) == null && fields.get(field).getDescribe().isUpdateable()) {
                        master.put(field, duplicateLeadValues.get(field));
                    }
                }
            }
            master.Potential_Duplicate__c = false;
            master.Merge_Process_Done__c = true;
            // Merge leads into master
            List<Database.MergeResult> mergeResults = Database.merge(master, duplicatesLeads, false);

            for(Database.MergeResult mergeResult : mergeResults){
                if (mergeResult.isSuccess()) {
                    // Get the master ID from the result and validate it
                    System.debug('Master record ID: ' + mergeResult.getId());

                    // Get the IDs of the merged records and display them
                    List<Id> mergedIds = mergeResult.getMergedRecordIds();
                    System.debug('IDs of merged records: ' + mergedIds);

                    // Get the ID of the re-parented record and validate that this the contact ID.
                    System.debug('Re-parented record ID: ' + mergeResult.getUpdatedRelatedIds());
                } else {
                    String errorMessage = '';
                    for (Database.Error err : mergeResult.getErrors()) {
                        errorMessage += errorMessage;
                        System.debug(err.getMessage());
                    }
                    throw new AuraHandledException('Something went wrong: ' + errorMessage);
                }
            }
        } else {
            throw new AuraHandledException('Unfortunately, there was a problem. Please try again. ' +
                    'If the problem continues, get in touch with your administrator!');
        }
    }

    // Edited method
    @TestVisible
    private static List<SObject> getSObjectRecordByIdWithAllAvailableFields(String objectAPIName, String recordId, Set<Id> objectIds) {
        Map<String, Schema.SObjectType> schemaMap = Schema.getGlobalDescribe();
        Map<String, Schema.SObjectField> fieldMap = schemaMap.get(objectAPIName).getDescribe().fields.getMap();
        List<String> fieldsToQuery = new List<String>();
        String soqlQuery = 'SELECT ' ;
        for (String s : fieldMap.keySet()) {
            if (fieldMap.get(s).getDescribe().isAccessible() && fieldMap.get(s).getDescribe().isUpdateable()) {
                fieldsToQuery.add(s);
            }
        }
        soqlQuery += String.join(fieldsToQuery, ',');

        // add the record id or set of ids to the query
        if(objectIds != null){
            soqlQuery += ' FROM ' + objectAPIName + ' WHERE Id IN :objectIds';
        } else {
            soqlQuery += ' FROM ' + objectAPIName + ' WHERE Id = \'' + String.escapeSingleQuotes(recordId) + '\'';
        }
        List<SObject> records = Database.query(soqlQuery);
        return records;
    }

    // New method
    // get the duplicates leads without the master record
    private static List<Lead> getDuplicates(Id masterId, List<Lead> leads){
        List<Lead> duplicates = new List<Lead>();
        for (Lead lead : leads) {
            if (lead.Id != masterId) {
                duplicates.add(lead);
            }
        }
        return duplicates;
    }

    // New method
    // get the ids of the duplicates leads without the master record
    private static Set<Id> getDuplicateLeadIds(List<Lead> leads){
        Set<Id> leadIds = new Set<Id>();
        for (Lead lead : leads) {
            leadIds.add(lead.Id);
        }
        return leadIds;
    }
